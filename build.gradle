import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        gradlePluginPortal()
        google()
    }
    dependencies {
        classpath libs.gradle
        classpath libs.grgit.gradle
        classpath libs.org.jacoco.core
        classpath libs.kotlin.gradle.plugin
        classpath libs.kotlin.serialization.gradle
    }
}

try {
    def grgit = Grgit.open(currentDir: project.rootDir)
    def lastCommit = grgit.head()

    project.ext.versionNum = grgit.log(includes:['HEAD']).size()
    project.ext.versionName = grgit.describe().split('-', 2).first()
    project.ext.versionDate = lastCommit.getDate()
    if (project.ext.versionName == null) {
        project.ext.versionName = 'DEV'
    }
} catch (Exception ignored) {
    project.ext.versionNum = 1
    project.ext.versionName = 'DEV'
    project.ext.versionDate = new Date()
}

project.ext {
    minSdkVersion = 26
    compileSdkVersion = 34

    googleVersions = [
        glide                 : '4.12.0',
        material              : '1.3.0',
    ]

    androidXVersions = [
        annotation            : '1.2.0',
        appCompat             : '1.3.0',
        browser               : '1.3.0',
    ]

    okioVersion = '2.10.0'
    jodaVersion = '2.10.10'
}

task showVersion {
    doLast {
        logger.lifecycle("Version ID: " + project.versionNum)
        logger.lifecycle("Version Name: " + project.versionName)
        logger.lifecycle("Version Date: " + project.versionDate)
    }
}
